// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:

Or16(a=x , b= true, out= noteverythingx);
Not16(in= noteverythingx, out= xzero);
Or16(a=y , b= true, out= noteverythingy);
Not16(in= noteverythingy, out= yzero);

Mux16(a= x, b= xzero, sel=zx , out=xafterzx);
Mux16(a= y, b= yzero, sel=zy , out=yafterzy);
Not16(in= xafterzx, out= notx);
Not16(in= yafterzy, out= noty);


Mux16(a= xafterzx, b= notx, sel= nx, out= xtouseforf);
Mux16(a= yafterzy, b= noty, sel= ny, out= ytouseforf);

And16(a= xtouseforf, b=ytouseforf , out= xANDy);
Add16(a = xtouseforf, b =ytouseforf , out = xADDy);
Mux16(a= xANDy, b= xADDy, sel= f, out= fout);

Not16(in= fout, out= notfout);
Mux16(a=fout , b=notfout, sel=no , out=out, out[15]=ng, out[0..7]=half, out[8..15]=secondhalf);

Or8Way(in= half, out=firsthalfiszero );
Or8Way(in= secondhalf, out= secondhalfiszero);

Or(a= firsthalfiszero, b= secondhalfiszero, out= zrforinput);
Not(in= zrforinput, out= zr);

  
    
}