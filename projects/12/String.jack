// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
/**
 * Represents character strings. In addition for constructing and disposing
 * strings, the class features methods for getting and setting individual
 * characters of the string, for erasing the string's last character,
 * for appending a character to the string's end, and more typical
 * string-oriented operations.
 */
class String {

   field Array chars;
   field int currentLen;
   field int maxLen;

    /** constructs a new empty string with a maximum length of maxLength
     *  and initial length of 0. */     
    constructor String new(int maxLength) {
        var int max;
        if (maxLength = 0) {
        let max = 1;
        } else {
            let max = maxLength;
        }
        let chars = Array.new(max);
        let currentLen = 0;
        let maxLen = max;
        return this;
    }

    /** Disposes this string. */
    method void dispose() {
        do chars.dispose();
        do Memory.deAlloc(this);
        return;
    }

    /** Returns the current length of this string. */
    method int length() {

    return currentLen;

    }

    /** Returns the character at the j-th location of this string. */
    method char charAt(int j) {

        return chars[j];
    }


    /** Sets the character at the j-th location of this string to c. */
    method void setCharAt(int j, char c) {
    
        let chars[j] = c;
        return;
    }

    /** Appends c to this string's end and returns this string. */
    method String appendChar(char c) {
        if(currentLen < maxLen) {
        do setCharAt(currentLen, c);
        let currentLen = currentLen + 1;
        }
        return this;
    
    }

    /** Erases the last character from this string. */
    method void eraseLastChar() {
        if (currentLen = 0){
            return;
        }
        let chars[currentLen - 1] = 0;
        let currentLen = currentLen - 1;
        return;
    }

    /** Returns the integer value of this string, 
     *  until a non-digit character is detected. */
    method int intValue() {
        var int i, value, numsLen, number;
        var boolean neg;
        let i = 0;
    let value = 0;
        if (chars[i] = 45) {
            let neg = true;
            let i = 1;
        } else {
            let neg = false;
        }

    while (i < currentLen) {
        let number = chars[i] - 48;
        if((number < 10) & ~(number < 0)){
                 let value = (value * 10) + number;
        } else {
            let i = currentLen; // break;
        }
        let i = i + 1;
   
    }
    
        if (neg) {
            return -value;
        }

    return value;
}

    /** Sets this string to hold a representation of the given value. */
    method void setInt(int i) {
        var int numToPrint, num, counter, neg,digit;
        var Array singularNums;
        let currentLen = 0; // "deleted the old string";
        let singularNums = Array.new(13); // safe enough for size
        let counter = 0;

       

       if (i = 0) {
        do singularNums.dispose();
        let chars[0] = 48; // num 0;
        let currentLen = 1;
        return;
    }

    let num = i;
    let neg = 0;


    if (i < 0) {
        let neg = 1; // we have a negative number
         let num = -i; // make it positive // breaks if i = -32768 - will fix
         do appendChar(45); // the minus sign

    }

        while (num > 0){
        let numToPrint = num - (10 * (num / 10));
        let singularNums[counter] = numToPrint;
        let num = num / 10;
        let counter = counter + 1;
        }

    
    

        while( counter > 0) {
            let counter = counter - 1;
            let digit = singularNums[counter];
            if (digit < 10) {
                do appendChar(digit + 48); 
            }
           
        }
       do singularNums.dispose();
        return;

    }

    /** Returns the new line character. */
    function char newLine() {
        return 128;
    }

    /** Returns the backspace character. */
    function char backSpace() {
        return 129;
    }

    /** Returns the double quote (") character. */
    function char doubleQuote() {
        return 34;
    }
}
